buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'me.tatarka:gradle-retrolambda:3.2.5'
  }
}

repositories {
  mavenCentral()
  maven {
    url "https://s3-ap-southeast-1.amazonaws.com/godel-release/godel/"
  }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

android {
  signingConfigs {
    config {
      keyAlias 'jaydp.com'
      keyPassword '123456'
      storeFile file('signing_keys.jks')
      storePassword '123456'
    }
  }

  compileSdkVersion 24
  buildToolsVersion "24.0.1"
  defaultConfig {
    applicationId "com.jaydp.samplepayment"
    minSdkVersion 15
    targetSdkVersion 24
    versionCode 1
    versionName "1.0"
    multiDexEnabled true
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  dataBinding {
    enabled = true
  }
  buildTypes {
    release {
      signingConfig signingConfigs.config
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

def daggerVersion = '2.2'
def appCompatVersion = '24.1.0'
def playServiceVersion = '9.2.1'

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])

  compile "com.android.support:appcompat-v7:$appCompatVersion"
  compile "com.android.support:design:$appCompatVersion"
  compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha3'
  compile 'com.android.support:multidex:1.0.1'

  retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.3.0'

  /** Google Play Services */
  compile "com.google.android.gms:play-services-maps:$playServiceVersion"
  compile "com.google.android.gms:play-services-location:$playServiceVersion"

  /** Dagger 2 */
  compile "com.google.dagger:dagger:$daggerVersion"
  apt "com.google.dagger:dagger-compiler:$daggerVersion"
  provided 'javax.annotation:jsr250-api:1.0'

  testCompile 'junit:junit:4.12'
  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
}

/** reads private values (keys) from app/gradle.properties and builds them into BuildConfig */
android.buildTypes.all { buildType ->
  project.properties.any { property ->
    if (property.key.toLowerCase().startsWith("str.")) {
      buildType.buildConfigField "String",
          property.key.replace("str.", "").replace(".", "_").toUpperCase(),
          "\"${property.value}\""
    }
  }
}